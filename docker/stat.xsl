<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

<xsl:template match="/">
    <html>
        <head>
            <title>RTMP statistics</title>
            <style>
                body { font-family: "Helvetica Neue", Helvetica, Arial, sans-serif; background: #f8f9fa; margin: 20px; color: #333; }
                table { width: 100%; border-collapse: collapse; background: #fff; margin-bottom: 20px; box-shadow: 0 1px 3px rgba(0,0,0,0.1); }
                th, td { padding: 12px; border: 1px solid #dee2e6; text-align: left; }
                th { background: #6c757d; color: #fff; border-color: #6c757d; }
                
                .header { background: #6c757d; color: white; padding: 15px; margin-bottom: 20px; border-radius: 4px; }
                
                /* 統一的列樣式 */
                .app-row td { background-color: #e9ecef; color: #495057; font-weight: bold; }
                .type-row td { background-color: #f8f9fa; color: #6c757d; text-align: center; font-style: italic; }
                
                tr.stream-row { cursor: pointer; transition: background-color 0.2s; }
                tr.stream-row:hover { background-color: #e2e6ea; }
                
                .client-table { margin: 10px; width: 98%; box-shadow: none; border: 1px solid #eee; }
                .client-table th { background: #6c757d; font-size: 0.85em; padding: 8px; border-color: #6c757d; }
                .client-table td { font-size: 0.85em; padding: 8px; background: #fff; }
            </style>
        </head>
        <body>
            <div class="header">
                <h1>Nginx RTMP 統計面板</h1>
                <p>Nginx 版本: <xsl:value-of select="/rtmp/nginx_version"/> | 運行時間: <xsl:value-of select="/rtmp/uptime"/> 秒</p>
            </div>

            <xsl:apply-templates select="rtmp"/>
            
            <hr/>
            <div style="font-size: small; color: #6c757d; margin-top: 20px;">
                Generated by <a href='https://github.com/arut/nginx-rtmp-module' style="color: #6c757d; text-decoration: none;">nginx-rtmp-module</a>&#160;<xsl:value-of select="/rtmp/nginx_rtmp_version"/>,
                <a href="http://nginx.org" style="color: #6c757d; text-decoration: none;">nginx</a>&#160;<xsl:value-of select="/rtmp/nginx_version"/>,
                pid <xsl:value-of select="/rtmp/pid"/>,
                built <xsl:value-of select="/rtmp/built"/>&#160;<xsl:value-of select="/rtmp/compiler"/>
            </div>
        </body>
    </html>
</xsl:template>

<xsl:template match="rtmp">
    <table cellspacing="1" cellpadding="5">
        <thead>
            <tr>
                <th>RTMP</th>
                <th>#clients</th>
                <th colspan="4">Video</th>
                <th colspan="4">Audio</th>
                <th>In bytes</th>
                <th>Out bytes</th>
                <th>In bits/s</th>
                <th>Out bits/s</th>
                <th>State</th>
                <th>Time</th>
            </tr>
        </thead>
        <tbody>
            <tr>
                <td colspan="2">Accepted: <xsl:value-of select="naccepted"/></td>
                <th style="background: #6c757d;">codec</th>
                <th style="background: #6c757d;">bits/s</th>
                <th style="background: #6c757d;">size</th>
                <th style="background: #6c757d;">fps</th>
                <th style="background: #6c757d;">codec</th>
                <th style="background: #6c757d;">bits/s</th>
                <th style="background: #6c757d;">freq</th>
                <th style="background: #6c757d;">chan</th>
                <td>
                    <xsl:call-template name="showsize">
                        <xsl:with-param name="size" select="bytes_in"/>
                    </xsl:call-template>
                </td>
                <td>
                    <xsl:call-template name="showsize">
                        <xsl:with-param name="size" select="bytes_out"/>
                    </xsl:call-template>
                </td>
                <td>
                    <xsl:call-template name="showsize">
                        <xsl:with-param name="size" select="bw_in"/>
                        <xsl:with-param name="bits" select="1"/>
                        <xsl:with-param name="persec" select="1"/>
                    </xsl:call-template>
                </td>
                <td>
                    <xsl:call-template name="showsize">
                        <xsl:with-param name="size" select="bw_out"/>
                        <xsl:with-param name="bits" select="1"/>
                        <xsl:with-param name="persec" select="1"/>
                    </xsl:call-template>
                </td>
                <td/>
                <td>
                    <xsl:call-template name="showtime">
                        <xsl:with-param name="time" select="/rtmp/uptime * 1000"/>
                    </xsl:call-template>
                </td>
            </tr>
            <xsl:apply-templates select="server"/>
        </tbody>
    </table>
</xsl:template>

<xsl:template match="server">
    <xsl:apply-templates select="application"/>
</xsl:template>

<xsl:template match="application">
    <tr class="app-row">
        <td colspan="16">
            <b><xsl:value-of select="name"/></b>
        </td>
    </tr>
    <xsl:apply-templates select="live"/>
    <xsl:apply-templates select="play"/>
</xsl:template>

<xsl:template match="live">
    <tr class="type-row">
        <td colspan="16">
            <i>live streams</i> (<xsl:value-of select="nclients"/>)
        </td>
    </tr>
    <xsl:apply-templates select="stream"/>
</xsl:template>

<xsl:template match="play">
    <tr class="type-row">
        <td colspan="16">
            <i>vod streams</i> (<xsl:value-of select="nclients"/>)
        </td>
    </tr>
    <xsl:apply-templates select="stream"/>
</xsl:template>

<xsl:template match="stream">
    <tr class="stream-row">
        <td>
            <a href="">
                <xsl:attribute name="onclick">
                    var d=document.getElementById('<xsl:value-of select="../../name"/>-<xsl:value-of select="name"/>');
                    d.style.display=d.style.display=='none'?'':'none';
                    return false
                </xsl:attribute>
                <xsl:value-of select="name"/>
                <xsl:if test="string-length(name) = 0">
                    [EMPTY]
                </xsl:if>
            </a>
        </td>
        <td align="middle"> <xsl:value-of select="nclients"/> </td>
        <td>
            <xsl:value-of select="meta/video/codec"/>&#160;<xsl:value-of select="meta/video/profile"/>&#160;<xsl:value-of select="meta/video/level"/>
        </td>
        <td>
            <xsl:call-template name="showsize">
                <xsl:with-param name="size" select="bw_video"/>
                <xsl:with-param name="bits" select="1"/>
                <xsl:with-param name="persec" select="1"/>
            </xsl:call-template>
        </td>
        <td>
            <xsl:apply-templates select="meta/video/width"/>
        </td>
        <td>
            <xsl:value-of select="meta/video/frame_rate"/>
        </td>
        <td>
            <xsl:value-of select="meta/audio/codec"/>&#160;<xsl:value-of select="meta/audio/profile"/>
        </td>
        <td>
            <xsl:call-template name="showsize">
                <xsl:with-param name="size" select="bw_audio"/>
                <xsl:with-param name="bits" select="1"/>
                <xsl:with-param name="persec" select="1"/>
            </xsl:call-template>
        </td>
        <td>
            <xsl:apply-templates select="meta/audio/sample_rate"/>
        </td>
        <td>
            <xsl:value-of select="meta/audio/channels"/>
        </td>
        <td>
            <xsl:call-template name="showsize">
               <xsl:with-param name="size" select="bytes_in"/>
           </xsl:call-template>
        </td>
        <td>
            <xsl:call-template name="showsize">
                <xsl:with-param name="size" select="bytes_out"/>
            </xsl:call-template>
        </td>
        <td>
            <xsl:call-template name="showsize">
                <xsl:with-param name="size" select="bw_in"/>
                <xsl:with-param name="bits" select="1"/>
                <xsl:with-param name="persec" select="1"/>
            </xsl:call-template>
        </td>
        <td>
            <xsl:call-template name="showsize">
                <xsl:with-param name="size" select="bw_out"/>
                <xsl:with-param name="bits" select="1"/>
                <xsl:with-param name="persec" select="1"/>
            </xsl:call-template>
        </td>
        <td><xsl:call-template name="streamstate"/></td>
        <td>
            <xsl:call-template name="showtime">
               <xsl:with-param name="time" select="time"/>
            </xsl:call-template>
        </td>
    </tr>
    <tr style="display:none">
        <xsl:attribute name="id">
            <xsl:value-of select="../../name"/>-<xsl:value-of select="name"/>
        </xsl:attribute>
        <td colspan="16">
            <table class="client-table">
                <thead>
                    <tr>
                        <th>Id</th>
                        <th>State</th>
                        <th>Address</th>
                        <th>Flash version</th>
                        <th>Page URL</th>
                        <th>SWF URL</th>
                        <th>Dropped</th>
                        <th>Timestamp</th>
                        <th>A-V</th>
                        <th>Time</th>
                    </tr>
                </thead>
                <tbody>
                    <xsl:apply-templates select="client"/>
                </tbody>
            </table>
        </td>
    </tr>
</xsl:template>

<xsl:template name="showtime">
    <xsl:param name="time"/>

    <xsl:if test="$time &gt; 0">
        <xsl:variable name="sec">
            <xsl:value-of select="floor($time div 1000)"/>
        </xsl:variable>

        <xsl:if test="$sec &gt;= 86400">
            <xsl:value-of select="floor($sec div 86400)"/>d
        </xsl:if>

        <xsl:if test="$sec &gt;= 3600">
            <xsl:value-of select="(floor($sec div 3600)) mod 24"/>h
        </xsl:if>

        <xsl:if test="$sec &gt;= 60">
            <xsl:value-of select="(floor($sec div 60)) mod 60"/>m
        </xsl:if>

        <xsl:value-of select="$sec mod 60"/>s
    </xsl:if>
</xsl:template>

<xsl:template name="showsize">
    <xsl:param name="size"/>
    <xsl:param name="bits" select="0" />
    <xsl:param name="persec" select="0" />
    <xsl:variable name="sizen">
        <xsl:value-of select="floor($size div 1024)"/>
    </xsl:variable>
    <xsl:choose>
        <xsl:when test="$sizen &gt;= 1073741824">
            <xsl:value-of select="format-number($sizen div 1073741824,'#.###')"/> T</xsl:when>

        <xsl:when test="$sizen &gt;= 1048576">
            <xsl:value-of select="format-number($sizen div 1048576,'#.###')"/> G</xsl:when>

        <xsl:when test="$sizen &gt;= 1024">
            <xsl:value-of select="format-number($sizen div 1024,'#.##')"/> M</xsl:when>
        <xsl:when test="$sizen &gt;= 0">
            <xsl:value-of select="$sizen"/> K</xsl:when>
    </xsl:choose>
    <xsl:if test="string-length($size) &gt; 0">
        <xsl:choose>
            <xsl:when test="$bits = 1">b</xsl:when>
            <xsl:otherwise>B</xsl:otherwise>
        </xsl:choose>
        <xsl:if test="$persec = 1">/s</xsl:if>
    </xsl:if>
</xsl:template>

<xsl:template name="streamstate">
    <xsl:choose>
        <xsl:when test="active">active</xsl:when>
        <xsl:otherwise>idle</xsl:otherwise>
    </xsl:choose>
</xsl:template>


<xsl:template name="clientstate">
    <xsl:choose>
        <xsl:when test="publishing">publishing</xsl:when>
        <xsl:otherwise>playing</xsl:otherwise>
    </xsl:choose>
</xsl:template>


<xsl:template match="client">
    <tr>
        <td><xsl:value-of select="id"/></td>
        <td><xsl:call-template name="clientstate"/></td>
        <td>
            <a target="_blank">
                <xsl:attribute name="href">
                    http://apps.db.ripe.net/search/query.html&#63;searchtext=<xsl:value-of select="address"/>
                </xsl:attribute>
                <xsl:attribute name="title">whois</xsl:attribute>
                <xsl:value-of select="address"/>
            </a>
        </td>
        <td><xsl:value-of select="flashver"/></td>
        <td>
            <a target="_blank">
                <xsl:attribute name="href">
                    <xsl:value-of select="pageurl"/>
                </xsl:attribute>
                <xsl:value-of select="pageurl"/>
            </a>
        </td>
        <td><xsl:value-of select="swfurl"/></td>
        <td><xsl:value-of select="dropped"/></td>
        <td><xsl:value-of select="timestamp"/></td>
        <td><xsl:value-of select="avsync"/></td>
        <td>
            <xsl:call-template name="showtime">
               <xsl:with-param name="time" select="time"/>
            </xsl:call-template>
        </td>
    </tr>
</xsl:template>

<xsl:template match="publishing">
    publishing
</xsl:template>

<xsl:template match="active">
    active
</xsl:template>

<xsl:template match="width">
    <xsl:value-of select="."/>x<xsl:value-of select="../height"/>
</xsl:template>

</xsl:stylesheet>
